plugins {
    id 'com.android.application'
}

android {
    namespace "com.termoneplus"

    compileSdk rootProject.ext.compileSdk
    buildToolsVersion = rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        applicationId "com.termoneplus"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        vectorDrawables.useSupportLibrary = true

        versionCode 501
        versionName '5.0.1'

        manifestPlaceholders.applicationIdSuffix = ""
        buildConfigField("String", "APPLICATION_ID_SUFFIX", "\"\"")
        externalNativeBuild {
            cmake {
                arguments "-DAPPLICATION_ID:STRING=${applicationId}"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file(KEYSF)
            storePassword KEYSP
            keyAlias KEYKA
            keyPassword KEYKP
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "application_version", variant.versionName
    }
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')

    implementation libs.appcompat
    implementation libs.material
    implementation libs.preference
    implementation libs.annotation
}
